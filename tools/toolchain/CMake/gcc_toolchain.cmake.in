# Without that flag CMake is not able to pass test compilation check
set(CMAKE_TRY_COMPILE_TARGET_TYPE STATIC_LIBRARY)

if( "@MYOS_ARCH@" STREQUAL "x86")
    set(CMAKE_SYSTEM_PROCESSOR "x86")
    set(tripple i686-elf)
else()
    message(FATAL_ERROR "Architecture \"@MYOS_ARCH@\" not supported!")
endif()

set(TOOLCHAIN_PATH ${MYOS_SOURCE_DIR}/tools/toolchain/local/@MYOS_ARCH@/bin)
set(TOOLCHAIN_PREFIX ${TOOLCHAIN_PATH}/${tripple}-)

set(CMAKE_AR                        ${TOOLCHAIN_PREFIX}ar CACHE INTERNAL "")
set(CMAKE_ASM_COMPILER              ${TOOLCHAIN_PREFIX}gcc CACHE INTERNAL "")
set(CMAKE_C_COMPILER                ${TOOLCHAIN_PREFIX}gcc CACHE INTERNAL "")
set(CMAKE_CXX_COMPILER              ${TOOLCHAIN_PREFIX}g++ CACHE INTERNAL "")
set(CMAKE_LINKER                    ${TOOLCHAIN_PREFIX}ld CACHE INTERNAL "")
set(CMAKE_OBJCOPY                   ${TOOLCHAIN_PREFIX}objcopy CACHE INTERNAL "")
set(CMAKE_RANLIB                    ${TOOLCHAIN_PREFIX}ranlib CACHE INTERNAL "")
set(CMAKE_SIZE                      ${TOOLCHAIN_PREFIX}size CACHE INTERNAL "")
set(CMAKE_STRIP                     ${TOOLCHAIN_PREFIX}strip CACHE INTERNAL "")

set(CMAKE_C_FLAGS                   "-ffreestanding" CACHE INTERNAL "")
set(CMAKE_CXX_FLAGS                 "${CMAKE_C_FLAGS} -fno-exceptions" CACHE INTERNAL "")

set(CMAKE_C_FLAGS_DEBUG             "-Os -g" CACHE INTERNAL "")
set(CMAKE_C_FLAGS_RELEASE           "-Os -DNDEBUG" CACHE INTERNAL "")
set(CMAKE_CXX_FLAGS_DEBUG           "${CMAKE_C_FLAGS_DEBUG}" CACHE INTERNAL "")
set(CMAKE_CXX_FLAGS_RELEASE         "${CMAKE_C_FLAGS_RELEASE}" CACHE INTERNAL "")

set(CMAKE_EXE_LINKER_FLAGS          "-nostdlib -nostdinc -lgcc" )

# Find crtbegin.o and crtend.o
execute_process(
    COMMAND 
        ${CMAKE_C_COMPILER} ${CMAKE_C_FLAGS} -print-file-name=crtbegin.o
    OUTPUT_VARIABLE CRTBEGIN_OBJ
    OUTPUT_STRIP_TRAILING_WHITESPACE
    )
    
execute_process(
    COMMAND 
        ${CMAKE_C_COMPILER} ${CMAKE_C_FLAGS} -print-file-name=crtend.o
    OUTPUT_VARIABLE CRTEND_OBJ
    OUTPUT_STRIP_TRAILING_WHITESPACE
    )

# Object 'crtbegin.o' will be linked before all other objects.
# Object 'crtend.o' will be linked after all other objects.
SET(CMAKE_C_LINK_EXECUTABLE 
    "<CMAKE_C_COMPILER> <FLAGS> <CMAKE_C_LINK_FLAGS> <LINK_FLAGS> ${CRTBEGIN_OBJ} <OBJECTS> ${CRTEND_OBJ} -o <TARGET> <LINK_LIBRARIES>"
    )

# search programs in the host environment
set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)

# search headers and libraries in the target environment
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)